buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}

apply plugin: 'net.minecraftforge.gradle'

version = "0.13"
group= "requious"
archivesBaseName = "Requious Frakto"

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

minecraft {
    mappings channel: 'stable', version: '39-1.12'
    accessTransformer = file('/src/main/resources/META-INF/requious_at.cfg')

    runs {
        client {
            workingDirectory project.file('run_client')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            mods {
                requious {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run_server')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            mods {
                requious {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven {
        url = uri("https://cursemaven.com")
    }
    maven {
        url 'https://maven.blamejared.com'
    }
    maven {
        url "https://dvs1.progwml6.com/files/maven"
    }
    flatDir { dirs 'libs' }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'

    compileOnly("CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.+")
    compileOnly(fg.deobf("curse.maven:had-enough-items-557549:4810661"))
    compileOnly(fg.deobf("curse.maven:gregtech-ce-unofficial-557242:4799055"))
    compileOnly(fg.deobf("curse.maven:CodeChickenLib-242818:2779848"))

    implementation(fg.deobf("curse.maven:industrialcraft-2-242638:3078604"))
    implementation(fg.deobf("curse.maven:applied-energistics-2-223794:2747063"))
    implementation(fg.deobf("curse.maven:mystical-mechanics-api-300742:3006392"))
    implementation(fg.deobf("curse.maven:flux-networks-248020:3178199"))
}

afterEvaluate {
    tasks.findByName('prepareRuns')?.doLast {
        copy {
            from 'build/resources/main'
            into '/build/classes/java/main'
            duplicatesStrategy DuplicatesStrategy.INCLUDE
        }
    }
}

jar {
    manifest {
        attributes 'FMLAT': 'requious_at.cfg'
    }
    duplicatesStrategy DuplicatesStrategy.INCLUDE
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    duplicatesStrategy DuplicatesStrategy.INCLUDE
    classifier "dev"
}

artifacts {
    archives deobfJar
}
